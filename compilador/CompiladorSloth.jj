options{
	STATIC=false;
}
PARSER_BEGIN(CompiladorSloth)

import java.io.File;
import java.io.FileReader;
import java.io.BufferedReader;
import java.io.IOException;

public class CompiladorSloth
{
    private static Codigo cod = null;
    
	public static void main(String[] args) throws ParseException
	{
		System.out.println("\n--->  Sloth Compiler  <---\n");

		try{
			String fname = args[0];
            File f = new File(fname) ;
			FileReader fr = new FileReader(f);
			BufferedReader br = new BufferedReader(fr);
			cod = new Codigo(tokenImage, fname);

            CompiladorSloth cs = new CompiladorSloth(fr);
            cs.Programa();

			CodeTranslator ct = new CodeTranslator(cod, args[1]);
			ct.buildJava();
			ct.printCodeGen();
			System.out.println("File "+fname+" was successful compiled");
        }
		catch(ParseException e)
		{
			System.out.println("Compiler Error: An error occurred.");
			System.out.println(e.getMessage());
		}
		catch (IOException e) {
      		System.out.println("File Error: An error occurred.");
      		System.out.println(e.getMessage());
    	}
		catch(ArrayIndexOutOfBoundsException e){
			System.out.println("Argument Error: No file at program argument.");
		}
		System.out.println();
		//cod.printTokens();
		//cod.printVars();
	}
}
PARSER_END(CompiladorSloth)

SKIP:
{ 
	" "|"\n"|"\r"|"\r\n" 
}


TOKEN:
{
	<#LETRA: (["a"-"z","A"-"Z"])>
	|<#DIGITO: (["0" - "9"])>
	|<#CARACTERE: (<LETRA>|<DIGITO>)>
	|<#ASC: (["\0"-"!","$"-"&","("-"~"])> //tabela asc sem " ' #
}

TOKEN:
{
	<TIPOINT: "int">
	|<TIPODOUBLE: "double">
	|<TIPOCHAR: "char">
	|<TIPOSTRING: "string">
	|<TIPOBOOLEAN: "boolean">
	|<TIPOVOID: "void">
	|<RETORNO: "return">
}

TOKEN:
{
	<TRUE: "true">
	|<FALSE: "false">
}

TOKEN:
{
	<ENDL: "\\n">
	|<EOL: ";">
}

TOKEN:
{
	<ABREVET: "[">
	|<FECHAVET: "]">
}

TOKEN:
{
	<ADD: "+">
	|<SUB: "-">
	|<MULT: "*">
	|<MOD: "//">
	|<DIV: "/">
	|<POW: "^">
	|<PORCE: "%">
	|<ABREPAR: "(">
	|<FECHAPAR: ")">
	|<PONTO: ".">
}

TOKEN:
{
	<IGUAL: "==">
	|<DIF: "=!">
	|<MAIOR: ">">
	|<MENOR: "<">
	|<MAIGUAL: "=>">
	|<MEIGUAL: "=<">
	|<IGUALDADE: "=">
}

TOKEN:
{
	<AND: "&&">
	|<OR: "||">
	|<NOT: "!">
}

TOKEN:
{
	<IF: "when">
	|<ELSE: "otherwise">
}

TOKEN:
{	
	<FOR: "for">
	|<SEPFOR: ":">
	|<WHILE: "while">
	|<FOREACH: "foreach">
}

TOKEN:
{
	<FIRST: "first">
	|<LAST: "last">
	|<BEGIN: "begin">
	|<END: "end">
}

TOKEN:
{
	<IMPORT: "import">
	|<GET: "get">
	|<WRITE: "write">
	|<COMENT: "#"(<ASC>|"\""|"\'")*"#">
	|<SEPFUN: ",">
}

TOKEN:
{
	<NOMEVAR: <LETRA>(<CARACTERE>)*>
}

TOKEN:
{
	<STRING: "\""(<ASC>)*"\"">
	|<INTEIRO: (<DIGITO>)+>
	|<CARACTER: "'"<ASC>"'">
	|<REAL: <INTEIRO><PONTO><INTEIRO>>
	|<PORCENTAGEM: (<REAL>|<INTEIRO>)<PORCE>>
}

TOKEN:
{
	<UNKNOWMTOKEN: <ASC>>
}

void Programa():
{Token a,b;} // (((Importacao()|<COMENT>)*Execucao())|Execucao())(<COMENT>)*<EOF>
{
	(LOOKAHEAD(2) (Importacao()|(a=<COMENT>){cod.add(a);}|Funcao()))*
	[Execucao()]
	((b=<COMENT>){cod.add(b);})*
	<EOF>
}

void Funcao():
{Token a=null,b,c,d; Variable var=null;} // (<TIPOVOID>|TipoVar())NomeVar()<ABREPAR>[FuncPar()]<FECHAPAR>Bloco()
{
	((a=<TIPOVOID>){cod.add(a);cod.setHasReturn(true);}|a=TipoVar(){cod.openFunc();}){cod.setScope(a.kind);}
	(b=<NOMEVAR>){cod.add(b);var=cod.addDVarList(b.image, a.kind, b);var.setIsFunc(true);}
	(c=<ABREPAR>){cod.add(c);cod.openBloco();}
	[FuncPar(var)]
	(d=<FECHAPAR>){cod.add(d);}
	Bloco(){cod.closeBloco();cod.closeFunc(a);}
}

void FuncPar(Variable var):
{Token a, b;} // TipoVar()NomeVar()[<SEPFUN>FuncPar()]
{
	b=TipoVar(){var.addParameter(b.kind);}
	DeclaraVarFunc(b)
	[(a=<SEPFUN>){cod.add(a);}
	FuncPar(var)]
}

Variable DeclaraVarFunc(Token d):
{Token a,b,c,e; Variable var=null;} 	// <NOMEVAR>[(<ABREVET>Expressao()<FECHAVET>)]
{
	((a=<NOMEVAR>){
		cod.add(a);
		var=cod.addDVarList(a.image, d.kind, a);
		var.init();
	}
	[((b=<ABREVET>){cod.add(b); var.setIsVet(true);}
	(c=<FECHAVET>){cod.add(c);cod.initializeVar(a);})])
	{return var;}
}

void Retorno():
{Token a;} // <RETORNO>Expressao()
{
	(a=<RETORNO>){cod.add(a);
	cod.openExpressao(cod.getScope());
	cod.setHasReturn(true);}
	[a=Expressao()]{cod.closeExpressao(a);}
}

Token ChamaFuncao():
{Token a,b,exp;} // NomeVar()<ABREPAR>[ChamaFuncPar()]<FECHAPAR>
{
	(exp=<NOMEVAR>){cod.add(exp);cod.openChamaFunc(exp);}
	(a=<ABREPAR>){cod.add(a);}
	[ChamaFuncPar(exp)]
	(b=<FECHAPAR>){cod.add(b);
	cod.checkParameters(exp);
	return exp;}
}

void ChamaFuncPar(Token exp):
{Token a, b; int paramType;} // (NomeVar()|ValorVar())[<SEPFUN>FuncPar()]
{	
	{paramType=cod.getNextParam(exp);cod.openExpressao(paramType+9);}
	(b=Expressao()){cod.closeExpressao(b);}
	[(a=<SEPFUN>){cod.add(a);}
	ChamaFuncPar(exp)]
}

void Importacao():
{Token a,b;} //<IMPORT><STRING>
{
	(a=<IMPORT>)//{cod.add(a);}
	(b=<STRING>)//{cod.add(b);}
	{
		try{
			String fname = b.image;
			fname = fname.substring(0, fname.length()-1);
			fname = fname.substring(1);
            File f = new File(fname) ;

			cod.addFile(fname);
            CompiladorSloth cs = new CompiladorSloth(new FileReader(f));
            cs.Programa();
			cod.removeFile();
        }
		catch (IOException e) {
      		System.out.println("File Error: An error occurred.");
      		System.out.println(e.getMessage());
    	}
	}
}

void Execucao():
{Token a,b,c;} //<FIRST>(Linha())*<LAST>
{
	(a=<FIRST>){cod.add(a);
	cod.verificaFirst(a);
	cod.setScope(14);}
	(Linha()|(b=<COMENT>){cod.add(b);})*
	(c=<LAST>){cod.add(c);}
}

void Linha():
{Token a;} //((Write()|ManVar())<EOL>)|FluxoDados()
{
	((Write()|ManVar()|Retorno())(a=<EOL>){cod.add(a);})
	|FluxoDados()
}

void Write():
{Token a,b,c,d;} //<WRITE><ABREPAR>Expressao()<FECHAPAR>
{
	(a=<WRITE>){cod.add(a);}
	(b=<ABREPAR>){cod.add(b);
	cod.openExpressao(12);}
	(d=Expressao()){cod.closeExpressao(d);}
	(c=<FECHAPAR>){cod.add(c);}		
}

void ManVar():
{Token a,b;} //(TipoVar()NomeVar()[Atribuicao()])|(NomeVar()Atribuicao())|(ChamaFuncao())
{
	(a=TipoVar()ManVarSeq(a))
	| ( LOOKAHEAD(3) ((b=NomeVar())Atribuicao(cod.getVarType(b)+9){cod.initializeVar(b);})
	| ChamaFuncao())

}

void ManVarSeq(Token b):
{Token a;}
{
	DeclaraVar(b)
	[(a=<SEPFUN>){cod.add(a);}
	ManVarSeq(b)]
}

Token TipoVar():
{Token a=null;} // <TIPOBOOLEAN>|<TIPOCHAR>|<TIPODOUBLE>|<TIPOINT>|<TIPOSTRING>
{
	(a=<TIPOBOOLEAN>|a=<TIPOCHAR>|a=<TIPODOUBLE>|a=<TIPOINT>|a=<TIPOSTRING>)
	{
		cod.add(a);
		return a;
	}
}

Variable DeclaraVar(Token d):
{Token a,b,c,e; Variable var=null;} 	// <NOMEVAR>[(<ABREVET>Expressao()<FECHAVET>)]
{
	((a=<NOMEVAR>){
		cod.add(a);
		var=cod.addDVarList(a.image, d.kind, a);
	}
	[LOOKAHEAD(2) (((b=<ABREVET>){cod.add(b);
	var.setIsVet(true);
	cod.openExpressao(9);}
	(e=Expressao()){cod.closeExpressao(e);})
	(c=<FECHAVET>){cod.add(c);cod.initializeVar(a);}
	|(Atribuicao(d.kind)){cod.initializeVar(a);})])
	{return var;}
}

Token NomeVar():
{Token a,b,c,d; boolean vet = false; Variable var;} 	// <NOMEVAR>[(<ABREVET>Expressao()<FECHAVET>)]
{
	((a=<NOMEVAR>){var=cod.verifyVarList(a, false);
	cod.add(a);}
	[((b=<ABREVET>){cod.add(b);
	vet = true;
	cod.openExpressao(9);}
	(d=Expressao()){cod.closeExpressao(d);}
	(c=<FECHAVET>){cod.add(c);})])
	{var.checkVet(vet,a,cod.getFile());
	return a;}
}

void Atribuicao(int kind):
{Token a,b;} // <IGUALDADE>(Read()|Expressao())
{
	(a=<IGUALDADE>){cod.add(a);cod.openExpressao(kind);}((b=Read())|(b=Expressao())){cod.closeExpressao(b);}
}

Token Read():
{Token a,b,c,d=null;} // <GET><ABREPAR><FECHAPAR>
{
	(a=<GET>) {cod.add(a);}
	(b=<ABREPAR>) {cod.add(b);}
	[(d=TipoVar())]
	(c=<FECHAPAR>)
	{
		cod.add(c);
		if(d==null) cod.addToExp(TIPOSTRING);
		else cod.addToExp(d.kind);
		return a;
	}
}

Token Expressao():
{Token a,b;} /*
((<ABREPAR>(Expressao())+<FECHAPAR>)
| <NOT>Expressao()
| ValorVar()
| ChamaFuncao()
| NomeVar())
[LOOKAHEAD(2) Operador()Expressao()]
*/
{
	(((a=<ABREPAR>){cod.add(a);cod.openParExp(a);}
	(Expressao())+
	(b=<FECHAPAR>){cod.add(b);cod.closeParExp(a);})
	| (a=<NOT>){cod.add(a);cod.addOpToExp(a);}
	Expressao()
	| (a=ValorVar()){cod.addToExp(a);}
	| ((LOOKAHEAD(2)(a=ChamaFuncao())
	| ((a=NomeVar()){cod.checkInitVar(a);})){cod.addToExp(a);}))
	[LOOKAHEAD(2) Operador()Expressao()]
	{return a;}
}

void Operador():
{Token a=null;} // OpArit()|OpRelac()
{
	((a=OpArit())|(a=OpRelac()))
	{cod.addOpToExp(a);}
}

Token OpArit():
{Token a;} // <ADD>|<SUB>|<MOD>|<DIV>|<POW>|<MULT>
{
	((a = <ADD>){cod.add(a);}|
	(a = <SUB>){cod.add(a);}|
	(a = <MOD>){cod.add(a);}|
	(a = <DIV>){cod.add(a);}|
	(a = <POW>){cod.add(a);}|
	(a = <MULT>){cod.add(a);})
	{return a;}
}

Token OpRelac():
{Token a;} // <AND>|<OR>|<XOR>|<IGUAL>|<DIF>|<MAIGUAL>|<MEIGUAL>|<MAIOR>|<MENOR>
{
	((a=<AND>){cod.add(a);}
	|(a=<OR>){cod.add(a);}
	|(a=<IGUAL>){cod.add(a);}
	|(a=<DIF>){cod.add(a);}
	|(a=<MAIGUAL>){cod.add(a);}
	|(a=<MEIGUAL>){cod.add(a);}
	|(a=<MAIOR>){cod.add(a);}
	|(a=<MENOR>){cod.add(a);})
	{return a;}
	
}

Token ValorVar():
{Token a;} // <INTEIRO>|<REAL>|<CARACTER>|Booleano()|<PORCENTAGEM>|<STRING>
{
	((a=<INTEIRO>){cod.add(a);}
	|(a=<REAL>){cod.add(a);}
	|(a=<CARACTER>){cod.add(a);}
	|(a=Booleano())
	|(a=<PORCENTAGEM>){cod.add(a);}
	|(a=<STRING>){cod.add(a);})
	{return a;}
}

Token Booleano():
{Token a;} // <TRUE>|<FALSE>
{
	((a = <TRUE>){cod.add(a);}|
	(a = <FALSE>){cod.add(a);})
	{return a;}
}

void FluxoDados():
{} // While()|For()|Foreach()|If()
{
	{cod.openBloco();}
	(While()|For()|Foreach()|If())
	{cod.closeBloco();}
}

void While():
{Token a,b,c,d;} // <WHILE><ABREPAR>Expressao()<FECHAPAR>Bloco()
{
	(a=<WHILE>){cod.add(a);}
	(b=<ABREPAR>){cod.add(b);
	cod.openExpressao(13);}
	(d=Expressao()){cod.closeExpressao(d);}
	(c=<FECHAPAR>){cod.add(c);}
	Bloco()
}

void For():
{Token a,b,c,d,e,f,g;}/*
<FOR><ABREPAR>NomeVar()<IGUALDADE>
(ValorVar()|NomeVar())<SEPFOR>
(ValorVar()|NomeVar())<SEPFOR>
(ValorVar()|NomeVar())<FECHAPAR>Bloco()
*/
{
	(a=<FOR>){cod.add(a);}
	(b=<ABREPAR>){cod.add(b);}

	(f=<TIPOINT>){cod.add(f);}
	(g=<NOMEVAR>){cod.add(g);cod.addDVarList(g.image,f.kind, g);}
	Atribuicao(f.kind){cod.initializeVar(g);}

	(d=<SEPFOR>){cod.add(d);cod.openExpressao(9);}
	(c=Expressao()){cod.closeExpressao(c);}

	(d=<SEPFOR>){cod.add(d);cod.openExpressao(9);}
	(c=Expressao()){cod.closeExpressao(c);}
	(e=<FECHAPAR>){cod.add(e);}
	Bloco()
}

void Foreach():
{Token a,b,c,d,e,f;} // <FOREACH><ABREPAR>NomeVar()<SEPFOR>NomeVar()<FECHAPAR>Bloco()
{
	(a=<FOREACH>){cod.add(a);}
	(b=<ABREPAR>){cod.add(b);}
	(f=<NOMEVAR>){cod.add(f);}
	(c=<SEPFOR>){cod.add(c);}
	(e=<NOMEVAR>){
		cod.add(e);
		cod.checkForeach(f, e);
	}
	(d=<FECHAPAR>){cod.add(d);}
	Bloco()
}

void If():
{Token a,b,c,d,e;} // <IF><ABREPAR>Expressao()<FECHAPAR>Bloco()[<ELSE>Bloco()]
{
	(a=<IF>){cod.add(a);}
	(b=<ABREPAR>){cod.add(b);
	cod.openExpressao(13);}
	(e=Expressao()){cod.closeExpressao(e);}
	(c = <FECHAPAR>){cod.add(c);}
	Bloco()
	[(d = <ELSE>){cod.add(d);}Bloco()]
}

void Bloco():
{Token a,b,c;} //<BEGIN>(Linha()|<COMENT>)*<END>
{
	(a=<BEGIN>){cod.add(a);}
	(Linha()|(b=<COMENT>){cod.add(b);})*
	(c=<END>){cod.add(c);}
}
