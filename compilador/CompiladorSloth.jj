PARSER_BEGIN(CompiladorSloth)

public class CompiladorSloth
{
    private static Codigo cod = new Codigo();
    
	public static void main(String[] args) throws ParseException
	{
		try
		{
			System.out.println("\n--->  ECOM06WORK  <---\n");
			CompiladorSloth analizador = new CompiladorSloth(System.in) ;
			analizador.Programa();
			System.out.println("\n--- Compilado sem erros ---");
		}
		catch(ParseException e)
		{
			System.out.println("Analyzer: errors were found!");
			System.out.println(e.getMessage());
		}
		finally{
			cod.printTokens(tokenImage);
			//cod.testaMetodo(tokenImage);
		}
	}
}
PARSER_END(CompiladorSloth)

SKIP:
{ 
	" "|"\n"|"\r"|"\r\n" 
}


TOKEN:
{
	<#LETRA: (["a"-"z","A"-"Z"])>
	|<#DIGITO: (["0" - "9"])>
	|<#CARACTERE: (<LETRA>|<DIGITO>)>
	|<#ASC: (["\0"-"!","$"-"&","("-"~"])> //tabela asc sem " ' #
}

TOKEN:
{
	<TIPOINT: "int">
	|<TIPODOUBLE: "double">
	|<TIPOCHAR: "char">
	|<TIPOSTRING: "string">
	|<TIPOBOOLEAN: "boolean">
	|<TIPOVOID: "void">
	|<RETORNO: "return">
}

TOKEN:
{
	<TRUE: "true">
	|<FALSE: "false">
}

TOKEN:
{
	<ENDL: "\\n">
	|<EOL: ";">
}

TOKEN:
{
	<ABREVET: "[">
	|<FECHAVET: "]">
}

TOKEN:
{
	<ADD: "+">
	|<SUB: "-">
	|<MULT: "*">
	|<MOD: "//">
	|<DIV: "/">
	|<POW: "^">
	|<PORCE: "%">
	|<ABREPAR: "(">
	|<FECHAPAR: ")">
	|<PONTO: ".">
}

TOKEN:
{
	<IGUAL: "==">
	|<DIF: "=!">
	|<MAIOR: ">">
	|<MENOR: "<">
	|<MAIGUAL: "=>">
	|<MEIGUAL: "=<">
	|<IGUALDADE: "=">
}

TOKEN:
{
	<AND: "&&">
	|<OR: "||">
	|<NOT: "!">
	|<XOR: "|+">
}

TOKEN:
{
	<IF: "when">
	|<ELSE: "otherwise">
}

TOKEN:
{	
	<FOR: "for">
	|<SEPFOR: ":">
	|<WHILE: "while">
	|<FOREACH: "foreach">
}

TOKEN:
{
	<FIRST: "first">
	|<LAST: "last">
	|<BEGIN: "begin">
	|<END: "end">
}

TOKEN:
{
	<IMPORT: "import">
	|<GET: "get">
	|<WRITE: "write">
	|<COMENT: "#"(<ASC>)*"#">
	|<SEPFUN: ",">
}

TOKEN:
{
	<NOMEVAR: <LETRA>(<CARACTERE>)*>
}

TOKEN:
{
	<STRING: "\""(<ASC>)*"\"">
	|<INTEIRO: (<DIGITO>)+>
	|<CARACTER: "'"<ASC>"'">
	|<REAL: <INTEIRO><PONTO><INTEIRO>>
	|<PORCENTAGEM: (<REAL>|<INTEIRO>)<PORCE>>
}

void Programa():
{Token a,b;} // (((Importacao()|<COMENT>)*Execucao())|Execucao())(<COMENT>)*<EOF>
{
	(Importacao()|(a=<COMENT>){cod.add(a);}|Funcao())*
	Execucao()
	((b=<COMENT>){cod.add(b);})*
	<EOF>
}

void Funcao():
{Token a=null,b;} // (<TIPOVOID>|TipoVar())NomeVar()<ABREPAR>[FuncPar()]<FECHAPAR>Bloco()
{
	((a=<TIPOVOID>){cod.add(a);}|a=TipoVar()){cod.setScope(a.kind);}
	DeclaraVar(a)
	(a=<ABREPAR>){cod.add(a);cod.openBloco();}
	[FuncPar()]
	(b=<FECHAPAR>){cod.add(b);}
	Bloco(){cod.closeBloco();}
}

void FuncPar():
{Token a, b;} // TipoVar()NomeVar()[<SEPFUN>FuncPar()]
{
	b=TipoVar()DeclaraVar(b)
	[(a=<SEPFUN>){cod.add(a);}
	FuncPar()]
}

void Retorno():
{Token a;} // <RETORNO>Expressao()
{
	(a=<RETORNO>){cod.add(a);}
	[Expressao()]
	{cod.pp();}
}

void ChamaFuncao():
{Token a,b;} // NomeVar()<ABREPAR>[ChamaFuncPar()]<FECHAPAR>
{
	NomeVar()
	(a=<ABREPAR>){cod.add(a);}
	[ChamaFuncPar()]
	(b=<FECHAPAR>){cod.add(b);}
}

void ChamaFuncPar():
{Token a;} // (NomeVar()|ValorVar())[<SEPFUN>FuncPar()]
{
	(NomeVar()|ValorVar())
	[(a=<SEPFUN>){cod.add(a);}
	ChamaFuncPar()]
}

void Importacao():
{Token a,b;} //<IMPORT><STRING>
{
	(a=<IMPORT>){cod.add(a);}
	(b=<STRING>){cod.add(b);}
}

void Execucao():
{Token a,b,c;} //<FIRST>(Linha())*<LAST>
{
	(a=<FIRST>){cod.add(a);
	cod.verificaFirst();
	cod.setScope(14);}
	(Linha()|(b=<COMENT>){cod.add(b);})*
	(c=<LAST>){cod.add(c);}
}

void Linha():
{Token a;} //((Write()|ManVar())<EOL>)|FluxoDados()
{
	((Write()|ManVar()|Retorno())(a=<EOL>){cod.add(a);})
	|FluxoDados()
}

void Write():
{Token a,b,c;} //<WRITE><ABREPAR>Expressao()<FECHAPAR>
{
	(a=<WRITE>){cod.add(a);}
	(b=<ABREPAR>){cod.add(b);}
	Expressao()
	(c=<FECHAPAR>){cod.add(c);}		
}

void ManVar():
{Token a;} //(TipoVar()NomeVar()[Atribuicao()])|(NomeVar()Atribuicao())|(ChamaFuncao())
{
	(a=TipoVar()ManVarSeq(a))
	| ( LOOKAHEAD(3) (NomeVar()Atribuicao())
	| ChamaFuncao())

}

void ManVarSeq(Token b):
{Token a;}
{
	DeclaraVar(b)
	[Atribuicao()]
	[(a=<SEPFUN>){cod.add(a);}
	ManVarSeq(b)]
}

Token TipoVar():
{Token a=null;} // <TIPOBOOLEAN>|<TIPOCHAR>|<TIPODOUBLE>|<TIPOINT>|<TIPOSTRING>
{
	(a=<TIPOBOOLEAN>|a=<TIPOCHAR>|a=<TIPODOUBLE>|a=<TIPOINT>|a=<TIPOSTRING>)
	{
		cod.add(a);
		return a;
	}
}

void DeclaraVar(Token d):
{Token a,b,c;} 	// <NOMEVAR>[(<ABREVET>Expressao()<FECHAVET>)]
{
	(a=<NOMEVAR>){
		cod.add(a);
		cod.addDVarList(a.image, d.image);
	}
	[((b=<ABREVET>){cod.add(b);}
	Expressao()
	(c=<FECHAVET>){cod.add(c);})]
}


void NomeVar():
{Token a,b,c;} 	// <NOMEVAR>[(<ABREVET>Expressao()<FECHAVET>)]
{
	(a=<NOMEVAR>){cod.verifyVarList(a);
	cod.add(a);}
	[((b=<ABREVET>){cod.add(b);}
	Expressao()
	(c=<FECHAVET>){cod.add(c);})]
}

void Atribuicao():
{Token a;} // <IGUALDADE>(Read()|Expressao())
{
	(a=<IGUALDADE>){cod.add(a);}(Read()|Expressao())
}

void Read():
{Token a,b,c;} // <GET><ABREPAR><FECHAPAR>
{
	(a=<GET>) {cod.add(a);}
	(b=<ABREPAR>) {cod.add(b);}
	(c=<FECHAPAR>) {cod.add(c);}
}

void Expressao():
{Token a,b,c;} /*
((<ABREPAR>(Expressao())+<FECHAPAR>)
| <NOT>Expressao()
| ValorVar()
| ChamaFuncao()
| NomeVar())
[LOOKAHEAD(2) Operador()Expressao()]
*/
{
	(((a=<ABREPAR>){cod.add(a);}(Expressao())+(b=<FECHAPAR>){cod.add(b);})
	| (c=<NOT>){cod.add(c);}Expressao()
	| ValorVar()
	| ( LOOKAHEAD(3) ChamaFuncao()
	| NomeVar()))
	[LOOKAHEAD(2) Operador()Expressao()]
}

void Operador():
{} // OpArit()|OpRelac()
{
	OpArit()|OpRelac()
}

void OpArit():
{Token a,b,c,d,e,f;} // <ADD>|<SUB>|<MOD>|<DIV>|<POW>|<MULT>
{
	(a = <ADD>){cod.add(a);}|
	(b = <SUB>){cod.add(b);}|
	(c = <MOD>){cod.add(c);}|
	(d = <DIV>){cod.add(d);}|
	(e = <POW>){cod.add(e);}|
	(f = <MULT>){cod.add(f);}

}

void OpRelac():
{Token a,b,c,d,e,f,g,h,i;} // <AND>|<OR>|<XOR>|<IGUAL>|<DIF>|<MAIGUAL>|<MEIGUAL>|<MAIOR>|<MENOR>
   // Talvez ele de um warning com o <MAIGUAL> e <MAIOR> e do menor
{
	(a=<AND>){cod.add(a);}
	|(b=<OR>){cod.add(b);}
	|(c=<XOR>){cod.add(c);}
	|(d=<IGUAL>){cod.add(d);}
	|(e=<DIF>){cod.add(e);}
	|(f=<MAIGUAL>){cod.add(f);}
	|(g=<MEIGUAL>){cod.add(g);}
	|(h=<MAIOR>){cod.add(h);}
	|(i=<MENOR>){cod.add(i);}
	
}

void ValorVar():
{Token a,b,c,d,e;} // <INTEIRO>|<REAL>|<CARACTER>|Booleano()|<PORCENTAGEM>|<STRING>
{
	(a=<INTEIRO>){cod.add(a);}
	|(b=<REAL>){cod.add(b);}
	|(c=<CARACTER>){cod.add(c);}
	|Booleano()
	|(d=<PORCENTAGEM>){cod.add(d);}
	|(e = <STRING>){cod.add(e);}
}

void Booleano():
{Token a,b;} // <TRUE>|<FALSE>
{
	(a = <TRUE>){cod.add(a);}|
	(b = <FALSE>){cod.add(b);}
}

void FluxoDados():
{} // While()|For()|Foreach()|If()
{
	{cod.openBloco();}
	(While()|For()|Foreach()|If())
	{cod.closeBloco();}
}

void While():
{Token a,b,c;} // <WHILE><ABREPAR>Expressao()<FECHAPAR>Bloco()
{
	(a=<WHILE>){cod.add(a);}
	(b=<ABREPAR>){cod.add(b);}
	Expressao()
	(c=<FECHAPAR>){cod.add(c);}
	Bloco()
}

void For():
{Token a,b,c,d,e,f;}/*
<FOR><ABREPAR>NomeVar()<IGUALDADE>
(ValorVar()|NomeVar())<SEPFOR>
(ValorVar()|NomeVar())<SEPFOR>
(ValorVar()|NomeVar())<FECHAPAR>Bloco()
*/
{
	(a=<FOR>){cod.add(a);}
	(b=<ABREPAR>){cod.add(b);}
	((f=TipoVar()DeclaraVar(f))|NomeVar())
	(c=<IGUALDADE>){cod.add(c);}
	(ValorVar()|NomeVar())
	(d=<SEPFOR>){cod.add(d);}
	(ValorVar()|NomeVar())
	(d=<SEPFOR>){cod.add(d);}
	(ValorVar()|NomeVar())
	(e = <FECHAPAR>){cod.add(e);}
	Bloco()
}

void Foreach():
{Token a,b,c,d;} // <FOREACH><ABREPAR>NomeVar()<SEPFOR>NomeVar()<FECHAPAR>Bloco()
{
	(a=<FOREACH>){cod.add(a);}
	(b=<ABREPAR>){cod.add(b);}
	NomeVar()
	(c=<SEPFOR>){cod.add(c);}
	NomeVar()
	(d=<FECHAPAR>){cod.add(d);}
	Bloco()
}

void If():
{Token a,b,c,d;} // <IF><ABREPAR>Expressao()<FECHAPAR>Bloco()[<ELSE>Bloco()]
{
	(a=<IF>){cod.add(a);}
	(b=<ABREPAR>){cod.add(b);}
	Expressao()
	(c = <FECHAPAR>){cod.add(c);}
	Bloco()
	[(d = <ELSE>){cod.add(d);}Bloco()]
}

void Bloco():
{Token a,b,c;} //<BEGIN>(Linha()|<COMENT>)*<END>
{
	(a=<BEGIN>){cod.add(a);}
	(Linha()|(b=<COMENT>){cod.add(b);})*
	(c=<END>){cod.add(c);}
}
